/* Copyright Â© INFINI Ltd. All rights reserved.
 * Web: https://infinilabs.com
 * Email: hello#infini.ltd */


apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'elasticsearch.testclusters'
apply plugin: 'elasticsearch.rest-test'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jetbrains.kotlin.plugin.allopen'

esplugin {
  name 'cross-cluster-replication'
  description 'Cross Cluster Replication Plugin'
  classname 'org.elasticsearch.replication.ReplicationPlugin'
  extendedPlugins = ['index-management']

}

buildscript {
    ext {
        kotlin_version = System.getProperty("kotlin.version", "1.6.10")

    }

    repositories {
      mavenLocal()
      mavenCentral()
      maven { url "https://plugins.gradle.org/m2/" }
      maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
      // For local publish dependency
    }

    dependencies {
      classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
      classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlin_version}"
      classpath "org.jacoco:org.jacoco.agent:0.8.7"
    }
}

configurations.all {
  if (it.state != Configuration.State.UNRESOLVED) return
  resolutionStrategy {
    force "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    force "org.jetbrains.kotlin:kotlin-stdlib-common:${kotlin_version}"
    force 'junit:junit:4.13.1'
    force 'commons-beanutils:commons-beanutils:1.9.4'
    force 'com.google.guava:guava:30.0-jre'
    force 'com.puppycrawl.tools:checkstyle:8.29'
    force 'commons-codec:commons-codec:1.13'
    force 'org.apache.httpcomponents:httpclient:4.5.13'
    force 'org.apache.httpcomponents:httpclient-osgi:4.5.13'
    force 'org.apache.httpcomponents.client5:httpclient5:5.0.3'
    force 'org.apache.httpcomponents.client5:httpclient5-osgi:5.0.3'
    force "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    force "org.yaml:snakeyaml:${versions.snakeyaml}"
    force 'org.codehaus.plexus:plexus-utils:3.0.24'
  }
}

dependencies {
    compileOnly project(':plugins:index-management')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-common:${kotlin_version}"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.3.9'

    implementation "org.jetbrains:annotations:13.0"
    implementation "com.github.seancfoley:ipaddress:5.3.3"
    testImplementation "org.assertj:assertj-core:3.17.2"
    testImplementation "org.jetbrains.kotlin:kotlin-test:${kotlin_version}"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "org.mockito:mockito-core:4.7.0"
    testImplementation project(":test:framework")
    testImplementation project(':client:rest-high-level')

}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://d1nvenhzbhpy0q.cloudfront.net/snapshots/lucene/" }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs = ['-Xjsr305=strict']
    }
}

javadoc.enabled = false // turn off javadoc as it barfs on Kotlin code
