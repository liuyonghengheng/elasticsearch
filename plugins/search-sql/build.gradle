buildscript {
    ext {
        spring_version = "5.3.27"
    }
}

plugins {
    id "io.freefair.lombok" version "5.3.3.3"
}

esplugin {
  name 'sql'
  description 'Elasticsearch SQL'
  classname 'org.elasticsearch.sql.plugin.SQLPlugin'
}

// TODO: fix compiler warnings
compileJava.options.warnings = false
compileJava {
    options.compilerArgs.addAll(["-processor", 'lombok.launch.AnnotationProcessorHider$AnnotationProcessor'])
    doFirst {
        // TODO: do not fail build on warnings, need to fix all compiler warnings
        options.compilerArgs.remove('-Werror')
        // TODO: need to fix all java doc format
        options.compilerArgs.remove('-Xdoclint:all')
    }
}

configurations.all {
  resolutionStrategy.force 'junit:junit:4.13.2'
  // conflict with spring-jcl
  exclude group: "commons-logging", module: "commons-logging"
  // enforce 1.1.3, https://www.whitesourcesoftware.com/vulnerability-database/WS-2019-0379
  resolutionStrategy.force 'commons-codec:commons-codec:1.13'
  resolutionStrategy.force 'com.google.guava:guava:31.0.1-jre'
}

dependencies {
  compile 'org.springframework:spring-beans:5.3.27'
  compile project(":plugins:search-sql:common")
  compile project(':plugins:search-sql:legacy')
  compile project(':plugins:search-sql:search')
}


jar {
    archiveFileName = 'sql-plugin.jar'
}

tasks.named('javadocJar').configure {
    enabled = false
}

tasks.named('sourcesJar').configure {
    enabled = false
}
